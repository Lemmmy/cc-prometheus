buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.ajoberstar:gradle-git:1.6.0'
    }
}

plugins {
    id 'com.matthewprenger.cursegradle' version '1.0.10'
    id 'com.github.johnrengelman.shadow' version '2.0.3'
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = "1.0.0"
group = "org.squiddev"
archivesBaseName = "cc-prometheus"

minecraft {
    version = "1.12.2-14.23.4.2749"
    runDir = "run"
    replace '${version}', project.version

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20180724"
}

configurations {
    shadow
    compile.extendsFrom shadow
}

repositories {
    maven { url "https://cc.crzd.me/maven/" }
    maven { url "http://squiddev.cc/maven/" }
}

dependencies {
    // provided 'dan200.computercraft:ComputerCraft:1.80pr1-build5'
    compile 'org.squiddev:cc-tweaked:1.80pr1.8'

    shadow 'io.prometheus:simpleclient_common:0.0.26'
    shadow 'org.eclipse.jetty:jetty-server:9.4.6.v20170531'
    shadow 'javax.servlet:javax.servlet-api:4.0.0'
}

shadowJar {
    relocate 'org.eclipse.jetty', 'org.squiddev.cc_prometheus.jetty'
    relocate 'io.prometheus', 'org.squiddev.cc_prometheus.prometheus'
    relocate 'javax.servlet', 'org.squiddev.cc_prometheus.servlet'

    configurations = [ project.configurations.shadow ]

    exclude 'jetty-dir.css'
    exclude 'about.html'
}

reobf {
    shadowJar{}
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint"
    }
}

runClient.outputs.upToDateWhen { false }
runServer.outputs.upToDateWhen { false }
